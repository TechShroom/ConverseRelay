buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    
    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }
    dependencies {
        classpath group: 'com.techshroom', name: 'Aversion', version: '3.0.0'
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'aversion-util'
apply plugin: 'aversion-maven'
apply plugin: 'aversion-apt'

compileJava {
    options.incremental = true
}

mainClassName = 'me.kenzierocks.converse.ConverseRelay'

util {
    javaVersion = '1.8'
    extraContainers.add('org.eclipse.fx.ide.jdt.core.JAVAFX_CONTAINER')
}
mavencfg {
    description "An IRC Client written in Java."
    coords "TechShroom", "ConverseRelay"
}

idea.project.languageLevel = util.javaVersion

// Surprise JFX!
eclipse.project {
    natures 'org.eclipse.xtext.ui.shared.xtextNature'
    natures 'org.eclipse.buildship.core.gradleprojectnature'
    buildCommand 'org.eclipse.xtext.ui.shared.xtextBuilder'
    buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
}
// I don't even.
task eclipseSettings() << {
    def json = """|{
                  |  "1.0": {
                  |    "project_path": ":",
                  |    "project_dir": "${project.file('.')}",
                  |    "connection_project_dir": "${project.file('.')}",
                  |    "connection_gradle_user_home": null,
                  |    "connection_gradle_distribution": "GRADLE_DISTRIBUTION(WRAPPER)",
                  |    "connection_java_home": null,
                  |    "connection_jvm_arguments": "",
                  |    "connection_arguments": ""
                  |  }
                  |}\n""".stripMargin()
    // Write it all to gradle prefs
    file('.settings/gradle.prefs').write json
}
getTasksByName('eclipse', true).each {
    it.dependsOn eclipseSettings
}

version = '0.0.1-SNAPSHOT'
group = 'me.kenzierocks'

repositories {
    mavenCentral()
    maven {
        name "snapshots"
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}
configurations.all {
   resolutionStrategy {
     // don't cache changing modules at all
     cacheChangingModulesFor 0, 'seconds'
   }
}
def LOGBACK_VERSION = '1.1.7'
def SLF_VERSION = '1.7.21'
dependencies {
    compile group: 'ch.qos.logback', name: 'logback-core', version: LOGBACK_VERSION
    compile group: 'ch.qos.logback', name: 'logback-classic', version: LOGBACK_VERSION
    // Because why not?
    runtime group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.5'
    compile group: 'org.slf4j', name: 'slf4j-api', version: SLF_VERSION
    compile group: 'org.kitteh.irc', name: 'client-lib', version: '2.3.0'
    // compile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.5'
    // compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.4.4'
    // compile group: 'com.firebase', name: 'firebase-client-jvm', version: '2.5.+'
    // Sorta just for here.
    // compile group: 'com.firebase', name: 'firebase-token-generator', version: '2.0.+'
    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    compile group: 'com.techshroom', name: 'jsr305-plus', version: '0.0.1'
    // LOL AJE
    compile group: 'com.yuvimasory', name: 'orange-extensions', version: '1.3.0'
    // prettty
    compile group: 'com.aquafx-project', name: 'aquafx', version: '0.1'
    compile group: 'org.aerofx', name: 'aerofx', version: '0.2'
    addAPTReqWComp group: 'com.google.guava', name: 'guava', version: '19.0'
    addAPT group: 'com.google.auto.factory', name: 'auto-factory', version: '1.0-SNAPSHOT'
    addAPT group: 'com.google.auto.service', name: 'auto-service', version: '1.0-SNAPSHOT'
    addAPT group: 'com.google.auto.value', name: 'auto-value', version: '1.4-SNAPSHOT'
    addAPTReq name: 'aopalliance'
    addAPTReq name: 'asm'
    addAPTReq name: 'auto-value'
    addAPTReq name: 'auto-common'
    addAPTReq name: 'cglib'
    addAPTReq name: 'commons-collections'
    addAPTReq name: 'commons-lang'
    addAPTReq name: 'dagger'
    addAPTReq name: 'javawriter'
    addAPTReq name: 'javax.inject'
    addAPTReq name: 'velocity'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport
